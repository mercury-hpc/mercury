#------------------------------------------------------------------------------
# Include source and build directories
#------------------------------------------------------------------------------
set(MCHECKSUM_BUILD_INCLUDE_DEPENDENCIES
  ${CMAKE_CURRENT_SOURCE_DIR}
  ${CMAKE_CURRENT_BINARY_DIR}
)

#------------------------------------------------------------------------------
# External dependencies
#------------------------------------------------------------------------------
# ZLIB
option(MCHECKSUM_USE_ZLIB "Use ZLIB checksum." OFF)
if(MCHECKSUM_USE_ZLIB)
  find_package(ZLIB REQUIRED)
  if(ZLIB_FOUND)
    message(STATUS "ZLIB include directory: ${ZLIB_INCLUDE_DIRS}")
    set(MCHECKSUM_HAS_ZLIB 1)
    set(MCHECKSUM_INT_INCLUDE_DEPENDENCIES
      ${MCHECKSUM_INT_INCLUDE_DEPENDENCIES}
      ${ZLIB_INCLUDE_DIRS}
    )
    set(MCHECKSUM_EXT_LIB_DEPENDENCIES
      ${MCHECKSUM_EXT_LIB_DEPENDENCIES}
      ${ZLIB_LIBRARIES}
    )
  else()
    message(FATAL_ERROR "Could not find ZLIB, please check ZLIB_INCLUDE_DIRS.")
  endif()
endif()

# SSE 4.2
if(WIN32)
  set(MCHECKSUM_SSE4_2_OPTIONS /arch:SSE4.2)
else()
  set(MCHECKSUM_SSE4_2_OPTIONS -msse4.2)
endif()
try_compile(MCHECKSUM_CAN_COMPILE_SSE4_2
  ${CMAKE_BINARY_DIR} ${MCHECKSUM_CMAKE_MODULE_PATH}/CheckSSE42.c
  COMPILE_DEFINITIONS ${MCHECKSUM_SSE4_2_OPTIONS}
  OUTPUT_VARIABLE CHECK_SSE4_2_OUTPUT)
option(MCHECKSUM_USE_SSE4_2 "Use SSE 4.2 instruction set." ${MCHECKSUM_CAN_COMPILE_SSE4_2})
if(MCHECKSUM_USE_SSE4_2)
  message(STATUS "Using SSE 4.2 instruction set.")
  if(NOT ${MCHECKSUM_CAN_COMPILE_SSE4_2})
    message(FATAL_ERROR "This platform is not capable of compiling with SSE 4.2 instructions.")
  endif()
  add_compile_options(${MCHECKSUM_SSE4_2_OPTIONS})
  set(MCHECKSUM_HAS_SSE4_2 1)
endif(MCHECKSUM_USE_SSE4_2)

# ISA-L
option(MCHECKSUM_USE_ISAL "Use Intel(R) ISA-L library." OFF)
if(MCHECKSUM_USE_ISAL)
  find_package(ISAL REQUIRED)
  if(ISAL_FOUND)
    message(STATUS "ISA-L include directory: ${ISAL_INCLUDE_DIRS}")
    set(MCHECKSUM_HAS_ISAL 1)
    set(MCHECKSUM_INT_INCLUDE_DEPENDENCIES
      ${MCHECKSUM_INT_INCLUDE_DEPENDENCIES}
      ${ISAL_INCLUDE_DIRS}
    )
    set(MCHECKSUM_EXT_LIB_DEPENDENCIES
      ${MCHECKSUM_EXT_LIB_DEPENDENCIES}
      ${ISAL_LIBRARIES}
    )
  else()
    message(FATAL_ERROR "Could not find ISAL, please check ISAL_INCLUDE_DIRS.")
  endif()
endif()

#------------------------------------------------------------------------------
# Configure module header files
#------------------------------------------------------------------------------
# Set unique var used in the autogenerated config file (symbol import/export)
configure_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/mchecksum_config.h.in
  ${CMAKE_CURRENT_BINARY_DIR}/mchecksum_config.h
)

#------------------------------------------------------------------------------
# Set sources
#------------------------------------------------------------------------------
set(MCHECKSUM_SRCS
  ${CMAKE_CURRENT_SOURCE_DIR}/mchecksum.c
  ${CMAKE_CURRENT_SOURCE_DIR}/mchecksum_crc16.c
  ${CMAKE_CURRENT_SOURCE_DIR}/mchecksum_crc32c.c
  ${CMAKE_CURRENT_SOURCE_DIR}/mchecksum_crc64.c
)

if(MCHECKSUM_HAS_ZLIB)
  set(MCHECKSUM_SRCS
    ${MCHECKSUM_SRCS}
    ${CMAKE_CURRENT_SOURCE_DIR}/mchecksum_zlib.c
  )
endif()

set(MCHECKSUM_HASH_TYPES crc16 crc32c crc64)
if(MCHECKSUM_HAS_ZLIB)
  set(MCHECKSUM_HASH_TYPES ${MCHECKSUM_HASH_TYPES} crc32 adler32)
endif()

#----------------------------------------------------------------------------
# Libraries
#----------------------------------------------------------------------------

# Clean up system include path first
foreach(item ${MCHECKSUM_SYSTEM_INCLUDE_PATH})
  if(MCHECKSUM_INT_INCLUDE_DEPENDENCIES)
    list(REMOVE_ITEM MCHECKSUM_INT_INCLUDE_DEPENDENCIES ${item})
  endif()
endforeach()

# mchecksum
add_library(mchecksum ${MCHECKSUM_SRCS})
target_include_directories(mchecksum
  PUBLIC  "$<BUILD_INTERFACE:${MCHECKSUM_BUILD_INCLUDE_DEPENDENCIES}>"
           $<INSTALL_INTERFACE:${MCHECKSUM_INSTALL_INCLUDE_INTERFACE}>
)
target_include_directories(mchecksum
  SYSTEM PRIVATE ${MCHECKSUM_INT_INCLUDE_DEPENDENCIES}
)
target_link_libraries(mchecksum
  ${MCHECKSUM_EXT_LIB_DEPENDENCIES}
)
mchecksum_set_lib_options(mchecksum "mchecksum" ${MCHECKSUM_LIBTYPE})
if(MCHECKSUM_ENABLE_COVERAGE)
  set_coverage_flags(mchecksum)
endif()

set(MCHECKSUM_EXPORTED_LIBS mchecksum ${MCHECKSUM_EXPORTED_LIBS})

#-----------------------------------------------------------------------------
# Specify project header files to be installed
#-----------------------------------------------------------------------------
set(MCHECKSUM_HEADERS
  ${CMAKE_CURRENT_SOURCE_DIR}/mchecksum.h
  ${CMAKE_CURRENT_SOURCE_DIR}/mchecksum_error.h
  ${CMAKE_CURRENT_BINARY_DIR}/mchecksum_config.h
)

#-----------------------------------------------------------------------------
# Add file(s) to CMake Install
#-----------------------------------------------------------------------------
install(
  FILES
    ${MCHECKSUM_HEADERS}
  DESTINATION
    ${MCHECKSUM_INSTALL_INCLUDE_DIR}
  COMPONENT
    headers
)

#---------------------------------------------------------------------------
# Add Target(s) to CMake Install
#---------------------------------------------------------------------------
install(
  TARGETS
    mchecksum
  EXPORT
    ${MCHECKSUM_EXPORTED_TARGETS}
  LIBRARY DESTINATION ${MCHECKSUM_INSTALL_LIB_DIR}
  ARCHIVE DESTINATION ${MCHECKSUM_INSTALL_LIB_DIR}
  RUNTIME DESTINATION ${MCHECKSUM_INSTALL_BIN_DIR}
)

# This may not be the conventional way of doing things but we need a dirty hack
# to have targets exported into multiple locations
if(MCHECKSUM_EXTERNALLY_CONFIGURED AND MCHECKSUM_EXTERNAL_EXPORTED_TARGETS)
install(
  TARGETS
    mchecksum
  EXPORT
    ${MCHECKSUM_EXTERNAL_EXPORTED_TARGETS}
  LIBRARY DESTINATION ${MCHECKSUM_INSTALL_LIB_DIR}
  ARCHIVE DESTINATION ${MCHECKSUM_INSTALL_LIB_DIR}
  RUNTIME DESTINATION ${MCHECKSUM_INSTALL_BIN_DIR}
)
endif()

#-----------------------------------------------------------------------------
# Add Target(s) to CMake Install for import into other projects
#-----------------------------------------------------------------------------
install(
  EXPORT
    ${MCHECKSUM_EXPORTED_TARGETS}
  DESTINATION
    ${MCHECKSUM_INSTALL_DATA_DIR}/cmake/${MCHECKSUM_PACKAGE}
  FILE
    ${MCHECKSUM_EXPORTED_TARGETS}.cmake
)

#-----------------------------------------------------------------------------
# Export all exported targets to the build tree for use by parent project
#-----------------------------------------------------------------------------
if(NOT MCHECKSUM_EXTERNALLY_CONFIGURED)
export(
  TARGETS
    ${MCHECKSUM_EXPORTED_LIBS}
  FILE
    ${MCHECKSUM_EXPORTED_TARGETS}.cmake
)
endif()

#------------------------------------------------------------------------------
# Set variables for parent scope
#------------------------------------------------------------------------------
set(MCHECKSUM_HASH_TYPES ${MCHECKSUM_HASH_TYPES} PARENT_SCOPE)

# Pkg-config configuration
if(NOT WIN32)
  if(CMAKE_BUILD_TYPE)
    string(TOLOWER ${CMAKE_BUILD_TYPE} lower_cmake_build_type)
  endif()

  # Mchecksum private library dependencies
  foreach(exported_lib ${MCHECKSUM_EXPORTED_LIBS})
    if(lower_cmake_build_type MATCHES "debug")
      get_target_property(MCHECKSUM_LIBRARY ${exported_lib} OUTPUT_DEBUG_NAME)
    else()
      get_target_property(MCHECKSUM_LIBRARY ${exported_lib} OUTPUT_RELEASE_NAME)
    endif()
    set(MCHECKSUM_LIBRARIES "${MCHECKSUM_LIBRARIES} -l${MCHECKSUM_LIBRARY}")
  endforeach()
  set(MCHECKSUM_LIBRARIES ${MCHECKSUM_LIBRARIES} PARENT_SCOPE)

  # Mchecksum external library dependencies
  # Need to generate -llib if not already passed
  foreach(lib_dep ${MCHECKSUM_EXT_LIB_DEPENDENCIES})
    # get library name
    get_filename_component(lib_name ${lib_dep} NAME_WE)
    if(lib_name MATCHES "^-l")
      # lib_name found is -lxxx
      set(MCHECKSUM_EXT_LIB_DEPENDENCIES_LIST ${MCHECKSUM_EXT_LIB_DEPENDENCIES_LIST} ${lib_name})
    else()
      # lib_name is /path/to/lib so get library path and name
      get_filename_component(lib_path ${lib_dep} PATH)
      string(REGEX REPLACE "^lib" "" lib_name ${lib_name})
      set(MCHECKSUM_EXT_LIB_DEPENDENCIES_LIST ${MCHECKSUM_EXT_LIB_DEPENDENCIES_LIST} -L${lib_path} -l${lib_name})
    endif()
  endforeach()
  if(MCHECKSUM_EXT_LIB_DEPENDENCIES_LIST)
    list(REMOVE_DUPLICATES MCHECKSUM_EXT_LIB_DEPENDENCIES_LIST)
  endif()
  foreach(lib_dep ${MCHECKSUM_EXT_LIB_DEPENDENCIES_LIST})
    set(MCHECKSUM_LIB_DEPENDENCIES "${MCHECKSUM_LIB_DEPENDENCIES} ${lib_dep}")
  endforeach()
  set(MCHECKSUM_LIB_DEPENDENCIES ${MCHECKSUM_LIB_DEPENDENCIES} PARENT_SCOPE)

  # External include dependencies
  if(MCHECKSUM_EXT_INCLUDE_DEPENDENCIES)
    list(REMOVE_DUPLICATES MCHECKSUM_EXT_INCLUDE_DEPENDENCIES)
  endif()
  foreach(inc_dep ${MCHECKSUM_EXT_INCLUDE_DEPENDENCIES})
    set(MCHECKSUM_INCLUDE_DEPENDENCIES "${MCHECKSUM_INCLUDE_DEPENDENCIES} -I${inc_dep}")
  endforeach()
  set(MCHECKSUM_INCLUDE_DEPENDENCIES ${MCHECKSUM_INCLUDE_DEPENDENCIES} PARENT_SCOPE)
endif()
